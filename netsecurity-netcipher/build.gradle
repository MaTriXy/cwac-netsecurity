apply plugin: 'com.android.library'

android {
  compileSdkVersion 28

  defaultConfig {
    minSdkVersion 17
    targetSdkVersion 28
    versionCode 1
    versionName "1.0"
  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
}

repositories {
  maven {
    url "https://s3.amazonaws.com/repo.commonsware.com"
  }
}

dependencies {
  debugApi project(":netsecurity")
  releaseApi "com.commonsware.cwac:netsecurity:0.5.0"
  api "info.guardianproject.netcipher:netcipher:2.0.0-alpha1"
  api "info.guardianproject.netcipher:netcipher-okhttp3:2.0.0-alpha1"
  api "com.squareup.okhttp3:okhttp:3.9.1"
}

if (project.hasProperty('PUBLISH_GROUP_ID')) {
  // from http://stackoverflow.com/a/19484146/115145

  android.libraryVariants.all { variant ->
    def name = variant.buildType.name
    if (name.equals(com.android.builder.core.BuilderConstants.DEBUG)) {
      return; // Skip debug builds.
    }
    def task = project.tasks.create "jar${name.capitalize()}", Jar
    task.dependsOn variant.javaCompile
    task.from variant.javaCompile.destinationDir
    task.baseName = "cwac-netsecurity-netcipher"
    task.version = PUBLISH_VERSION
    task.exclude('com/commonsware/cwac/**/BuildConfig.**')
  }

  task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
  }

  artifacts {
    archives sourcesJar
  }

  apply plugin: 'maven'

  uploadArchives {
    repositories.mavenDeployer {
      pom.groupId = PUBLISH_GROUP_ID
      pom.artifactId = 'netsecurity-netcipher'
      pom.version = PUBLISH_VERSION

      repository(url: LOCAL_REPO)
    }
  }
}
